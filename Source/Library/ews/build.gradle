apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply from: "../../../ci-build-support/gradle/versions.gradle"
apply plugin: 'cdpSCM'
apply plugin: 'maven-publish'

import com.philips.cdp.cdpSCM

def environment = 'production'
if (project.hasProperty('envCode')) {
    environment = envCode
}

def objcdp = new cdpSCM()
objcdp.setProjectDir(projectDir)
objcdp.setEnv(platform: 'android', env: environment)
version = objcdp.getVersion(versions.ews_ews)
archivesBaseName = "ews-android"
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'
    }
}

//start publishing
artifactory {
    contextUrl = objcdp.getArtifactoryContextUrl()
    publish {
        repository {
            repoKey = objcdp.getArtifactoryResolveRepoKey()
            username = objcdp.getArtifactoryUploadUser()
            password = objcdp.getArtifactoryUploadPwd()
            defaults {
                publishPom = true
                publishIvy = false
                publishBuildInfo = true
                publishArtifacts = true
                publications('aar')
                properties = ['git.commitid': objcdp.getCommitHashLong()]
            }
        }
    }
    resolve {
        repository {
            repoKey = objcdp.getArtifactoryResolveRepoKey()
        }
    }

    clientConfig.timeout = 600
    // Artifactory connection timeout (in seconds). The default timeout is 300 seconds.
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId group
            version = version
            artifactId archivesBaseName

            /*
            release in the path done here to prevent local publishing, possibility to publish only release
             which comes from jenkins
             */
            artifact("$buildDir/outputs/aar/${archivesBaseName}_release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                def resolvedArtifacts = configurations.compile.getResolvedConfiguration().getResolvedArtifacts()
                resolvedArtifacts.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleVersion.id.group)
                    dependencyNode.appendNode('artifactId', it.moduleVersion.id.name)
                    dependencyNode.appendNode('version', it.moduleVersion.id.version)
                }
            }
        }
    }
}

//end publishing

android {
    def assignedVersionNumber = version
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildtools
    publishNonDefault true
    defaultConfig {
        minSdkVersion versions.minsdk
        targetSdkVersion versions.target
        vectorDrawables.useSupportLibrary = true
        renderscriptTargetApi versions.target
        renderscriptSupportModeEnabled true
        multiDexEnabled true
        versionName assignedVersionNumber
        consumerProguardFiles('proguard-rules.txt', 'proguard-rules-lib.txt')
    }

    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    /** Proguard on library reference: https://developer.android.com/studio/projects/android-library.html#Considerations
     *  Also reference with DLS library
     *  Generated aar file keep proguard.txt which take part in proguard process when added in app
     **/
    buildTypes {
        debug {

        }
        release {

        }
    }

    lintOptions {
        abortOnError false
    }

    jacocoAndroidUnitTestReport {
        csv.enabled false
        html.enabled true
        xml.enabled false
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def libName = archivesBaseName
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def newName
            if (buildType.equalsIgnoreCase("debug")) {
                 newName = "${libName}_${buildType}_${assignedVersionNumber}.aar"
            } else {
                 newName = "${libName}_${buildType}.aar"
            }
            output.outputFile = new File(output.outputFile.parent, newName)
        }
    }
}


dependencies {
    apply from: "../../../ci-build-support/gradle/versions.gradle"
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android/Google
    compile "com.android.support:support-annotations:${versions.support}"
    compile "com.android.support:design:${versions.support}"
    compile "com.android.support:appcompat-v7:${versions.support}"
    compile "com.android.support:animated-vector-drawable:${versions.support}"
    compile "com.android.support:cardview-v7:${versions.support}"

    // Philips Dependencies
    def dp = project.ext.has('useprojectdependencies')

    if(dp) {
        project(':uikitLib')
    } else {
        compile(group: 'com.philips.cdp', name: 'uikitLib', version: versions.uit_uikitLib + objcdp.getVersionSuffix(), ext: 'aar')
            {
                transitive = true
            }
    }
    
    if(dp) {
        project(':commlib-lan')
    }    else {
        compile(group: 'com.philips.cdp', name: 'commlib-lan', version: versions.cml_commlib + objcdp.getVersionSuffix(), changing: true)
        {
            transitive = true
        }
    }

    if(dp) {
        project(':uid')
    } else {
        compile(group: 'com.philips.cdp', name: 'uid', version: versions.uid_uid + objcdp.getVersionSuffix(), ext: 'aar', changing: true) {
            transitive = true
        }
    }

    if(dp) {
        project(':IconFont')
    } else {
        compile(group: 'com.philips.cdp', name: 'IconFont', version: versions.icf_iconFont + objcdp.getVersionSuffix(), ext: 'aar', changing: true)
            {
                transitive = true
            }
    }
    

    if(dp) {
        project(':uAppFwLib')
    } else {
        compile(group: 'com.philips.cdp', name: 'uAppFwLib', version: versions.uaf_uAppFwLib + objcdp.getVersionSuffix(), changing: true) {
            transitive = true
        }
    }

    // External
    compile "com.google.dagger:dagger:${versionsExternal.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${versionsExternal.dagger}"
    compile "org.greenrobot:eventbus:${versionsExternal.eventBus}"
    compile "net.danlew:android.joda:${versionsExternal.joda}"

    // Test
    testCompile "junit:junit:${versionsExternal.junit}"
    testCompile "org.mockito:mockito-core:${versionsExternal.mockito}"
    testCompile "org.robolectric:robolectric:${versionsExternal.robolectric}"
    testCompile "org.powermock:powermock-module-junit4:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-api-mockito:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-module-junit4-rule:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-classloading-xstream:${versionsExternal.powerMock}"
    testCompile("com.nhaarman:mockito-kotlin:${versionsExternal.mockitoKotlin}") {
        exclude group: 'org.mockito'
    }
}
