apply plugin: 'com.android.application'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'de.felixschulze.gradle.hockeyapp'

apply from: "../scripts/versions.gradle"
apply from: "../scripts/task_verification.gradle"
apply from: "../scripts/task_reporting.gradle"

jacoco {
    toolVersion = versions.jacoco
}

buildscript {
    apply from: projectDir.absolutePath + '/../../../../../build-support/gradle/versions.gradle'

    repositories {
        maven {
            url 'http://artifactory-ehv.ta.philips.com:8082/artifactory/platform-jcenter-remote'
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
        maven {
            url "http://artifactory-ehv.ta.philips.com:8082/artifactory/platform-plugins-release-local"
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
    }

    dependencies {
        classpath "com.trickyandroid:jacoco-everywhere:0.2.1"
        classpath "com.android.tools.build:gradle:2.3.1"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.12"
        classpath(builddeps.cdpSCM)
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "de.felixschulze.gradle:gradle-hockeyapp-plugin:3.6"
    }
}

def environment = 'production'
if (project.hasProperty('envCode')) {
    environment = envCode
}

apply plugin: 'cdpSCM'
import com.philips.cdp.cdpSCM

def objcdp = new cdpSCM()
objcdp.setProjectDir(projectDir)
objcdp.setEnv(platform: 'android', env: environment)
archivesBaseName = "referenceApp"

allprojects {
    repositories {
        maven {
            url "http://artifactory-ehv.ta.philips.com:8082/artifactory/platform-plugins-release-local"
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
        maven {
            url objcdp.getArtifactoryUrl()
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }

        apply plugin: 'com.jfrog.artifactory'
        group = 'com.philips.cdp'
    }
}

artifactory {
    contextUrl = objcdp.getArtifactoryContextUrl()
    publish {
        repository {
            repoKey = objcdp.getArtifactoryPublishRepoKey()
            username = objcdp.getArtifactoryUploadUser()
            password = objcdp.getArtifactoryUploadPwd()
            defaults {
                publishPom = true
                publishIvy = false
                publishBuildInfo = true
                publishArtifacts = true
                publications('apk')
                properties = ['git.commitid': objcdp.getCommitHashLong()]
            }
        }
    }
    resolve {
        repository {
            repoKey = objcdp.getArtifactoryResolveRepoKey()
        }
    }
    clientConfig.timeout = 600
    // Artifactory connection timeout (in seconds). The default timeout is 300 seconds.
}

//=======To zip and upload "Documents/External" ==STARTS=============
def zipDoc = file(buildDir.name + "/distributions/$project.name-$version-docs" + '.zip')
def zipSrc = file(buildDir.name + "/distributions/$project.name-$version-src" + '.tgz')
artifacts {
    archives zipDoc
    archives zipSrc
}

task zipDocuments(type: Zip) {
    from files(projectDir.toString() + "/../../../Documents/External/") {
        baseName = project.name
        classifier 'docs'
    }
}

task tgzTask(type: Tar) {
    classifier = 'src'
    from projectDir
    include '**/*'
    exclude('build')
    extension = 'tgz'
    compression = Compression.GZIP
}
//=======To zip and upload "Documents/External" ==ENDS=============

publishing {
    publications {
        apk(MavenPublication) {
            groupId group
            version = version
            artifactId archivesBaseName

            artifact zipDocuments

            artifact("$projectDir/dependencies.lock") {
                classifier = '1stleveldependencies'
            }

            artifact("$buildDir/outputs/mapping/AppFrameworkHamburger/release/mapping.txt") {
                classifier = 'mapping'
            }

            artifact("$buildDir/outputs/apk/${archivesBaseName}-release.apk")

            pom.withXml {
                    //Creating additional node for dependencies
                    def dependenciesNode = asNode().appendNode('dependencies')

                    //Defining configuration names from which dependencies will be taken (debugCompile or releaseCompile and compile)
					
                    def configurationNames = ["releaseCompile", 'compile']

                    configurationNames.each { configurationName ->
                        configurations[configurationName].allDependencies.each {
                            if (it.group != null && it.name != null) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)                                
                            }
                        }
                    }
                }
        }
    }
}

hockeyapp {
    apiToken = "b9d6e2f453894b4fbcb161b33a94f6c8"
    commitSha = objcdp.getCommitHashLong()
    buildServerUrl = objcdp.getArtifactoryUrl()
    releaseType = 2 // alpha
    notify = 2
    mappingFileNameRegex = "mapping.txt"
    mandatory = 1
    status = 2
    notesType = 1
    notes = new File(projectDir, "/../../../Documents/External/ReleaseNotes.md").getText('UTF-8')
}

def buildType 

android {

	applicationVariants.all { variant ->
        buildType = variant.buildType.name // sets the current build type
		println "*****BuildType****"+ buildType
    }
	
    compileSdkVersion config.targetSdk
    buildToolsVersion config.buildTools

    defaultConfig {
        minSdkVersion config.minSdk
        targetSdkVersion config.targetSdk
        versionCode config.versionCode
        versionName version + "(" + objcdp.getCommitHash() + ")"
        vectorDrawables.useSupportLibrary = true
        renderscriptTargetApi config.renderScriptApi
        renderscriptSupportModeEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    apply from: "../scripts/proposition_specific.gradle"
    apply from: "../scripts/flavors.gradle"
    apply from: "../scripts/exclude_files.gradle"

    publishNonDefault true
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'

    exclude group: 'commons-codec', module: 'commons-codec'
    exclude module: 'httpcore'
}

dependencies {
    compile("com.android.support:support-annotations:${versionsAndroid.support}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compile "com.android.support:design:${versionsAndroid.support}"
    compile "com.android.support:appcompat-v7:${versionsAndroid.support}"

    debugCompile "com.squareup.leakcanary:leakcanary-android-no-op:${versionsExternal.leakCanary}"
    leakCanaryCompile "com.squareup.leakcanary:leakcanary-android:${versionsExternal.leakCanary}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${versionsExternal.leakCanary}"
    psraReleaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${versionsExternal.leakCanary}"

    debugCompile "com.facebook.stetho:stetho:${versionsExternal.stetho}"
    compile "com.j256.ormlite:ormlite-core:${versionsExternal.ormLite}"
    compile "com.j256.ormlite:ormlite-android:${versionsExternal.ormLite}"
    compile "com.android.support:multidex:${versionsAndroid.multidex}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versionsExternal.butterKnife}"

    compile("com.crittercism:crittercism-android-agent:${versionsExternal.crittercism}")

    def dp = project.ext.has('useprojectdependencies')

    if(dp) {
        debugCompile project(path: ':product-registration-lib', configuration: 'debug')
        releaseCompile project(path: ':product-registration-lib', configuration: 'release')
        debugCompile project(path: ':digitalCare', configuration: 'debug')
        releaseCompile project(path: ':digitalCare', configuration: 'release')
        debugCompile project(path: ':iap', configuration: 'debug')
        releaseCompile project(path: ':iap', configuration: 'release')
        debugCompile project(path: ':dataServices', configuration: 'debug')
        releaseCompile project(path: ':dataServices', configuration: 'release')
        debugCompile project(path: ':mya', configuration: 'debug')
        releaseCompile project(path: ':mya', configuration: 'release')
        debugCompile project(path: ':mya-mch', configuration: 'debug')
        releaseCompile project(path: ':mya-mch', configuration: 'release')
        debugCompile project(path: ':commlib', configuration: 'debug')
        releaseCompile project(path: ':commlib', configuration: 'release')
        debugCompile project(path: ':commlib-demouapp', configuration: 'debug')
        releaseCompile project(path: ':commlib-demouapp', configuration: 'release')
        debugCompile project(path: ':digitalCareUApp', configuration: 'debug')
        releaseCompile project(path: ':digitalCareUApp', configuration: 'release')
        debugCompile project(path: ':uAppFwLibUApp', configuration: 'debug')
        releaseCompile project(path: ':uAppFwLibUApp', configuration: 'release')
        debugCompile project(path: ':iapUApp', configuration: 'debug')
        releaseCompile project(path: ':iapUApp', configuration: 'release')
        debugCompile project(path: ':dataServicesUApp', configuration: 'debug')
        releaseCompile project(path: ':dataServicesUApp', configuration: 'release')
        debugCompile project(path: ':product-registration-libUApp', configuration: 'debug')
        releaseCompile project(path: ':product-registration-libUApp', configuration: 'release')
        debugCompile project(path: ':UserRegistrationUApp', configuration: 'debug')
        releaseCompile project(path: ':UserRegistrationUApp', configuration: 'release')
        debugCompile project(path: ':devicepairingUApp', configuration: 'debug')
        releaseCompile project(path: ':devicepairingUApp', configuration: 'release')
        debugCompile project(path: ':telehealth', configuration: 'debug')
        releaseCompile project(path: ':telehealth', configuration: 'release')
        debugCompile project(path: ':ewsUApp', configuration: 'debug')
        releaseCompile project(path: ':ewsUApp', configuration: 'release')
        debugCompile project(path: ':bluelib-demouapp', configuration: 'debug')
        releaseCompile project(path: ':bluelib-demouapp', configuration: 'release')
        debugCompile project(path: ':pushnotification', configuration: 'debug')
        releaseCompile project(path: ':pushnotification', configuration: 'release')
        debugCompile project(path: ':themesettings', configuration: 'debug')
        releaseCompile project(path: ':themesettings', configuration: 'release')
    } else {
        compile philipsdeps.product_registration_lib
        compile philipsdeps.digitalCare
        compile philipsdeps.iap
        compile (philipsdeps.dataServices) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.mya) {
            exclude group: 'com.android.support'
        }
        compile philipsdeps.mya_mch
        compile (philipsdeps.commlib) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.commlib_demouapp) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.digitalCareUApp) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.uAppFwLibUApp) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.iapUApp) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.dataServicesUApp) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.product_registration_libUApp) {
            exclude group: 'com.android.support'
        }
        compile (philipsdeps.UserRegistrationUApp) {
            exclude group: 'com.android.support'
            exclude group: 'com.philips.cdp', module: 'RegistrationApi'
        }
        compile (philipsdeps.devicepairingUApp) {
            exclude group: 'com.android.support'
        }
        compile philipsdeps.telehealth
        compile philipsdeps.ewsUApp
        compile philipsdeps.bluelib_demouapp
        compile project(':pushnotification')
        compile project(':themesettings')
    }

    testCompile "junit:junit:${versionsExternal.junit}"
    testCompile "org.khronos:opengl-api:${versionsExternal.opengl}"
    testCompile "org.robolectric:robolectric:${versionsExternal.robolectric}"
    testCompile "org.robolectric:shadows-multidex:${versionsExternal.robolectricSupport}"
    testCompile "org.robolectric:shadows-support-v4:${versionsExternal.robolectricSupport}"
    testCompile "org.mockito:mockito-all:${versionsExternal.mockitoAll}"

    testCompile "org.powermock:powermock-core:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-module-junit4:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-module-junit4-rule:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-module-junit4-rule-agent:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-api-mockito:${versionsExternal.powerMock}"
    testCompile "org.powermock:powermock-classloading-xstream:${versionsExternal.powerMock}"

    testCompile "org.json:json:${versionsExternal.json}"
}

task copyToLib(type: Copy) {
    into "$buildDir/outputs/lib"
    from configurations.compile
}

tasks.withType(Test) {
    ignoreFailures = true
    maxParallelForks = Runtime.runtime.availableProcessors()
    maxHeapSize = "2048m"
}

task('printArtifactoryApkPath') {
    doLast {
        if (new File('apkname.txt').exists()) {
            new File('apkname.txt').delete()
        }
        File apkname = new File('apkname.txt')
        apkname.append(objcdp.getArtifactoryContextUrl() +   \
          "/" + objcdp.getArtifactoryPublishRepoKey() +   \
          "/" + project.group.replace('.', '/') + "/" +   \
          archivesBaseName + "/" + version +   \
          "/" + archivesBaseName + "-" + version + ".apk")
        println "Artifactory path of apk stored in apkname.txt"
    }
}
