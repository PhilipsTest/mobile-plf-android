apply plugin: 'com.android.application'
apply plugin: 'spoon'

buildscript {
    repositories {
        maven { url 'http://130.145.245.193:8081/artifactory/jcenter' }

        jcenter()

    }

}


spoon{
//    className = 'com.philips.cdp.ui.catalogdls.activity.ToggleSwitchTest'
//    methodName = 'verifyToggleSwitchOrientationChange'
}

android {
    compileSdkVersion 24
    buildToolsVersion '24'
    defaultConfig {
        applicationId "com.philips.platform.catalogapp"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "0.1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        TestPhilipsStore {
            keyAlias 'philips'
            keyPassword 'philips'
            storeFile file('../UIT_GoogleBeta_KeyStore.jks')
            storePassword 'philips'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }

        jenkins {
            testCoverageEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'asm-license.txt'

    }
}

def coverageSourceDirs = ['src/main/java']

task jacocoTestReport(type: JacocoReport, dependsOn: "testJenkinsUnitTest") {
    group = "Reporting"

    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(dir: 'build/intermediates/classes/jenkins',
            excludes: ['**/R.class',
                       '**/R$*.class'])

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testJenkinsUnitTest.exec')

    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencies {
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    compile project(':uit')
    androidTestCompile 'org.apache.commons:commons-lang3:3.4'
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:24.0.0'
}