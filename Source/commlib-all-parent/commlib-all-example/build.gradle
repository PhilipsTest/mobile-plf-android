/*
 * (c) Koninklijke Philips N.V., 2017.
 *     All rights reserved.
 */
apply plugin: 'com.android.application'

buildscript { bs ->
    apply from: rootDir.absolutePath + '/../common/gradle/project-build.gradle', to: bs
    apply from: rootDir.absolutePath + '/../common/gradle/module.gradle'
    repositories {
        maven { url repos.jcenter }
    }
}

repositories {
    maven { url repos.jcenter }
    maven { url repos.release }
    maven { url repos.snapshot }
    maven { url repos.stage }
}

android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildtools

    defaultConfig {
        minSdkVersion versions.minsdk
        targetSdkVersion versions.sdk
        applicationId "com.philips.cdp2.commlib.example"
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    def commlib_ble = findProject(':commlib-ble') ? ':commlib-ble' : ':commlib-all'

    debugCompile project(path: commlib_ble, configuration: 'debug')
    releaseCompile project(path: commlib_ble, configuration: 'release')

    compile deps.support.appcompatv7
    compile deps.support.design

    compile group: 'joda-time', name: 'joda-time', version: '2.9.7'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile deps.junit
}
