apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.philips.cdp2.ews.demoapplication"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode getMyVersionCode()
        versionName "1.0"
    }
    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            storeFile file("demoapp-keystore.jks")
            storePassword "philips"
            keyAlias "philips"
            keyPassword "philips"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            pseudoLocalesEnabled true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def appName = "ews_demo_U_app_"
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def newName = "${appName}${gitBranch()}_${buildType}_${getMyVersionCode()}.apk"
            output.outputFile = new File(output.outputFile.parent, newName)
        }
    }
}

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch += line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch.replaceAll("/","-")
}

def getMyVersionCode() {
    def buildNumber = project.hasProperty('buildNumber') ? buildNumber.toInteger() : 1
    buildNumber
}

dependencies {
    apply from: "../../../ci-build-support/gradle/versions.gradle"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'joda-time:joda-time:2.9.9'
    // Philips Dependencies
    compile project(':ews')
    compile "com.android.support:support-annotations:${versions.support}"
    compile "com.android.support:animated-vector-drawable:${versions.support}"
    compile "com.android.support:cardview-v7:${versions.support}"
    compile 'com.android.support:multidex:1.0.2'
    compile "com.android.support:design:${versions.support}"
    compile "com.android.support:appcompat-v7:${versions.support}"
    compile 'com.google.dagger:dagger:2.9'
    annotationProcessor "com.google.dagger:dagger-compiler:2.9"
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
}