/*
 * Copyright (c) 2015-2018 Koninklijke Philips N.V.
 * All rights reserved.
 */

apply plugin: 'com.android.library'
apply plugin: 'pl.droidsonroids.pitest'

apply from: project.rootDir.absolutePath + '/build-support/gradle/cdpscm.gradle'
apply from: project.rootDir.absolutePath + '/build-support/gradle/lib.gradle'
apply from: project.rootDir.absolutePath + '/build-support/gradle/tasks.gradle'

archivesBaseName = "commlib-cloud"

android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildtools

    defaultConfig {
        multiDexEnabled true
        minSdkVersion versions.minsdk
        targetSdkVersion versions.target
        versionCode 1
        versionName version
        buildConfigField "String", "TLA", "\"cml\""
    }

    buildTypes {
        debug {
            buildConfigField "String", "LIBRARY_VERSION", "\"${version}(${objcdp.getCommitHash()})\""
        }
        release {
            consumerProguardFiles 'proguard-rules.pro'
            buildConfigField "String", "LIBRARY_VERSION", "\"${version}\""
        }
    }

    lintOptions {
        abortOnError false
    }

}

task generateJavadocPublicApi(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("${projectDir}/../../../Documents/External/${project.name}-api/")

    include {
        if (it.isDirectory) {
            return true
        }
        return it.file.text.contains("* @publicApi")
    }

    options {
        memberLevel = JavadocMemberLevel.PUBLIC
        links("http://docs.oracle.com/javase/7/docs/api/")
        linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")
        docEncoding = 'UTF-8'
        encoding = 'UTF-8'
        charSet = 'UTF-8'
    }

    failOnError false
}

task zipJavadoc(type: Zip) {
    from "${projectDir}/../../../Documents/External/${project.name}-api/"
    include '*'
    include '*/*' //to include contents of a folder present inside Reports directory
    archiveName "${project.name}-api.zip"
    destinationDir(file("${projectDir}/../../../Documents/External/"))
}

dependencies {
    compileOnly deps.support.annotations

    implementation deps.gson

    if (directdependencies) {
        implementation project(':commlib-api')
        implementation project(':cloudcontroller-api')
    } else {
        implementation philipsdeps.commlib_api
        implementation philipsdeps.cloudcontroller_api
    }

    if (directdependencies) {
        testImplementation project(':cloudcontroller')
    } else {
        testImplementation philipsdeps.cloudcontroller
    }

    testImplementation project(':commlib-testutils')

    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.assertj
}

pitest {
    targetClasses = [
            'com.philips.cdp.dicommclient.port.common.*',
            'com.philips.cdp2.commlib.cloud.*',
            'com.philips.cdp2.commlib.core.*',
            'com.philips.cdp2.commlib.lan.communication.*',
            'com.philips.cdp2.commlib.lan.context.*',
            'com.philips.cdp2.commlib.lan.discovery.*',
            'com.philips.cdp2.commlib.lan.security.*',
            'com.philips.cdp.dicommclient.request.*',
    ]  /* specify target classes to be mutated */
    excludedClasses = [
            'com.philips.cdp2.commlib.core.port.firmware.util.FirmwarePortStateWaiterTest',
            'com.philips.cdp.dicommclient.port.common.PairingHandlerTest',
    ]
    pitestVersion = versions.pitest
    avoidCallsTo = ['com.philips.cdp.dicommclient.util.DICommLog']
    threads = 1 /* specify number of threads */
    outputFormats = ['XML', 'HTML'] /* specify output format */
    reportDir = "${rootDir.absolutePath}/build/report/${project.name}/pitest/"
    timestampedReports = false
}
tasks.withType(Test) {
    ignoreFailures = true
}