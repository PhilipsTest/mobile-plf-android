/*
 * Copyright (c) 2015-2018 Koninklijke Philips N.V.
 * All rights reserved.
 */

apply plugin: 'com.android.library'
apply plugin: 'pl.droidsonroids.pitest'

apply from: project.rootDir.absolutePath + '/build-support/gradle/cdpscm.gradle'
apply from: project.rootDir.absolutePath + '/build-support/gradle/lib.gradle'
apply from: project.rootDir.absolutePath + '/build-support/gradle/tasks.gradle'

archivesBaseName = "commlib-api"

android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildtools

    defaultConfig {
        minSdkVersion versions.minsdk
        targetSdkVersion versions.target
        versionCode 1
        versionName version
        buildConfigField "String", "TLA", "\"cml\""
    }

    buildTypes {
        debug {
            buildConfigField "String", "LIBRARY_VERSION", "\"${version}(${objcdp.getCommitHash()})\""
        }
        release {
            consumerProguardFiles 'proguard-rules.pro'
            buildConfigField "String", "LIBRARY_VERSION", "\"${version}\""
        }
    }

    lintOptions {
        abortOnError false
    }

}

task generateJavadocPublicApi(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("${projectDir}/../../../Documents/External/${project.name}-api/")

    include {
        if (it.isDirectory) {
            return true
        }
        return it.file.text.contains("* @publicApi")
    }

    options {
        memberLevel = JavadocMemberLevel.PUBLIC
        links("http://docs.oracle.com/javase/7/docs/api/")
        linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")
        docEncoding = 'UTF-8'
        encoding = 'UTF-8'
        charSet = 'UTF-8'
    }

    failOnError false
}

dependencies {
    compileOnly deps.support.annotations

    implementation deps.gson

    def suffix = objcdp.getVersionSuffix()

    if (findProject(':commlib-testutils')) {
        testApi project(':commlib-testutils')
    } else {
        testApi("$group:commlib-testutils:$platformversion$suffix") { changing = true }
    }

    for (dep in [':AppInfra', ':securedblibrary']) {
        if (directdependencies) {
            implementation project(dep)
        } else {
            implementation("$group$dep:$platformversion$suffix") { changing = true }
        }
    }

    implementation deps.ormlite.core
    implementation deps.ormlite.android
    implementation deps.sqlcipher

    testImplementation deps.robolectric
    testImplementation deps.robolectric_shadows
    testImplementation deps.assertj
    testImplementation deps.junit
    testImplementation deps.powermock_junit
    testImplementation deps.powermock_api_mockito
}

pitest {
    targetClasses = [
            'com.philips.cdp.dicommclient.port.common.*',
            'com.philips.cdp2.commlib.cloud.*',
            'com.philips.cdp2.commlib.core.*',
            'com.philips.cdp2.commlib.lan.communication.*',
            'com.philips.cdp2.commlib.lan.context.*',
            'com.philips.cdp2.commlib.lan.discovery.*',
            'com.philips.cdp2.commlib.lan.security.*',
            'com.philips.cdp.dicommclient.request.*',
            'com.philips.cdp.dicommclient.networknode.*',
            'com.philips.cdp.dicommclient.security.*',
    ]  /* specify target classes to be mutated */
    excludedClasses = [
            'com.philips.cdp2.commlib.core.port.firmware.util.FirmwarePortStateWaiterTest',
            'com.philips.cdp.dicommclient.port.common.PairingHandlerTest',
            'com.philips.cdp2.commlib.core.appliance.ApplianceTest',
            'com.philips.cdp2.commlib.core.appliance.ApplianceManagerTest',
    ]
    pitestVersion = versions.pitest
    avoidCallsTo = ['com.philips.cdp.dicommclient.util.DICommLog']
    threads = 1 /* specify number of threads */
    outputFormats = ['XML', 'HTML'] /* specify output format */
    reportDir = "${rootDir.absolutePath}/build/report/${project.name}/pitest/"
    timestampedReports = false
}
tasks.withType(Test) {
    ignoreFailures = true
}