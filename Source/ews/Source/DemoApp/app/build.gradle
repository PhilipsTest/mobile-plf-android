apply plugin: 'com.android.application'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

buildscript {
    apply from: projectDir.absolutePath + '/../../../../../build-support/gradle/versions.gradle'

    repositories {
        maven { url 'http://artifactory-ehv.ta.philips.com:8082/artifactory/platform-jcenter-remote'
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
        maven {
            url "http://artifactory-ehv.ta.philips.com:8082/artifactory/platform-plugins-release-local"
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
    }

    dependencies {
        classpath builddeps.gradle
        classpath builddeps.buildinfoextractor
        classpath(builddeps.cdpSCM)
    }
}

def environment = 'production'
if (project.hasProperty('envCode')) {
    environment = envCode
}

apply plugin: 'cdpSCM'
import com.philips.cdp.cdpSCM
def objcdp = new cdpSCM()
objcdp.setProjectDir(projectDir)
objcdp.setEnv(platform: 'android', env: environment )
archivesBaseName = "ewsApp"

allprojects {
    repositories {
        maven {
            url "http://artifactory-ehv.ta.philips.com:8082/artifactory/platform-plugins-release-local"
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
        maven {
            url objcdp.getArtifactoryUrl()
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
        apply plugin: 'com.jfrog.artifactory'
        group = 'com.philips.cdp'

        flatDir {
            dirs 'libs'
        }
    }
}

artifactory {
    contextUrl = objcdp.getArtifactoryContextUrl()
    publish {
        repository {
            repoKey = objcdp.getArtifactoryPublishRepoKey()
            username = objcdp.getArtifactoryUploadUser()
            password = objcdp.getArtifactoryUploadPwd()
            defaults {
                publishPom = true
                publishIvy = false
                publishBuildInfo = true
                publishArtifacts = true
                publications ('aar')
                properties = ['git.commitid': objcdp.getCommitHashLong()]
            }
        }
    }
    resolve {
        repository {
            repoKey = objcdp.getArtifactoryResolveRepoKey()
        }
    }
    clientConfig.timeout = 600 // Artifactory connection timeout (in seconds). The default timeout is 300 seconds.
}


publishing {
    publications {
        aar(MavenPublication) {
            groupId group
            version = version
            artifactId archivesBaseName

            /*
            release in the path done here to prevent local publishing, possibility to publish only release
             which comes from jenkins
             */
            artifact("$buildDir/outputs/aar/${archivesBaseName}_release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                def resolvedArtifacts = configurations.implementation.allDependencies
                resolvedArtifacts.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildtools

    defaultConfig {
        applicationId "com.philips.platform.ews.demoapplication"
        minSdkVersion versions.minsdk
        targetSdkVersion versions.target
        versionCode 1
        versionName "1.0"
    }
    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            storeFile file("demoapp-keystore.jks")
            storePassword "philips"
            keyAlias "philips"
            keyPassword "philips"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            pseudoLocalesEnabled true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def appName = "ews_demo_app_"
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def newName = "${appName}${gitBranch()}_${buildType}_${getMyVersionCode()}.apk"
            output.outputFile = new File(output.outputFile.parent, newName)
        }
    }


}

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch += line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch.replaceAll("/", "-")
}

def getMyVersionCode() {
    def buildNumber = project.hasProperty('buildNumber') ? buildNumber.toInteger() : 1
    buildNumber
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Philips Dependencies
    implementation project(':ewsUApp')
    implementation "com.android.support:support-annotations:${versions.support}"
    implementation "com.android.support:animated-vector-drawable:${versions.support}"
    implementation "com.android.support:cardview-v7:${versions.support}"
    implementation deps.multidex
    implementation "com.android.support:design:${versions.support}"
    implementation deps.support.appcompatv7
    annotationProcessor deps.dagger.compiler
    implementation deps.dagger.dagger
    implementation deps.greenrobot
    implementation deps.constraintlayout
}


