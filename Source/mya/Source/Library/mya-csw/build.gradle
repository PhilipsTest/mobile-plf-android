/*
 * Copyright (c) 2015-2018 Koninklijke Philips N.V.
 * All rights reserved.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'pl.droidsonroids.pitest'

apply from: project.rootDir.absolutePath + '/build-support/gradle/cdpscm.gradle'
apply from: project.rootDir.absolutePath + '/build-support/gradle/lib.gradle'
apply from: project.rootDir.absolutePath + '/build-support/gradle/versions.gradle'

archivesBaseName = "mya-csw"

buildscript {
    dependencies {
        classpath deps.butterknife_gradle
        classpath builddeps.pitest
    }
}
android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildtools

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion versions.minsdk
        targetSdkVersion versions.target
        versionCode 1
        versionName version + "(" + objcdp.getCommitHash() + ")"
        multiDexEnabled true
        generatedDensities = []
        // replace the below string with your own Google client ID. Make sure this is consistent
        // with the values used in openid_appauth_idp_configs.xml
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'com.googleusercontent.apps.346000571262-m5u27lu130n1h1i2vbsggo34ae6nr1hp'
        ]

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
        }

    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    lintOptions {
        abortOnError false
    }

    publishNonDefault true
}

dependencies {
    //3rd party
    compile deps.gson

    compile deps.dagger.dagger
    compile deps.dagger.androidsupport
    annotationProcessor deps.dagger.androidprocessor
    annotationProcessor deps.dagger.compiler
    compile deps.butterknife
    annotationProcessor deps.butterknife_compiler

    // Philips Components
    def dp = project.ext.has('useprojectdependencies')
    if (dp) {
        debugCompile project(path: ':uid', configuration: 'debug')
        releaseCompile project(path: ':uid', configuration: 'release')
        debugCompile project(path: ':registrationApi', configuration: 'debug')
        releaseCompile project(path: ':registrationApi', configuration: 'release')
        debugCompile project(path: ':pif', configuration: 'debug')
        releaseCompile project(path: ':pif', configuration: 'release')
        debugCompile project(path: ':mya-catk', configuration: 'debug')
        releaseCompile project(path: ':mya-catk', configuration: 'release')
    } else {
        compile philipsdeps.uid
        compile (philipsdeps.registrationApi) {
            exclude group: 'com.android.support', module: 'customtabs'
            exclude group: 'com.google.dagger'
        }
        compile philipsdeps.pif
        compile philipsdeps.mya_catk
    }

    // Tests
    testCompile deps.junit
    testCompile deps.mockito
    testCompile deps.powermock_api_mockito
    testCompile deps.powermock_junit
    testCompile deps.assertj
    testCompile(deps.robolectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile deps.robolectric_shadows
    testCompile deps.powermock_junit_rule
    testCompile deps.powermock_junit_rule_agent
    testCompile deps.powermock_classloading_xstream
}

task updateResources(type: Exec) {

    workingDir "$rootDir"
    commandLine './ConsentWidgets/generateLocalization.sh'
    ignoreExitValue true
    doLast {
        println "$execResult"
        if (execResult.exitValue == 0) {
            throw new GradleException('error occurred while generating Localization files from Excel')
        }
    }
}

pitest {
    targetClasses = ['com.philips.platform.mya.csw.*']  //by default "${project.group}.*"
    pitestVersion = '1.1.0' //not needed when a default PIT version should be used
    threads = 1
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    excludedClasses = [
            '**Factory*', // Generated by Dagger
            '**_MembersInjector*', // Generated by Dagger
            'com.philips.platform.mya.csw.dialogs.ConfirmDialogViewTest', // Causes error but no tests fail (false positive)
            'com.philips.platform.mya.csw.dialogs.DialogViewTest', // Causes error but no tests fail (false positive)
            'com.philips.platform.mya.csw.dialogs.ProgressDialogViewTest', // Causes error but no tests fail (false positive)
            'com.philips.platform.mya.csw.injection.DaggerCswComponent', // Created by Dagger
            'com.philips.platform.mya.csw.injection.DaggerCswComponent$Builder' // Created by Dagger
    ]
}