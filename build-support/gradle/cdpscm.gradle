apply plugin: 'com.jfrog.artifactory'
apply plugin: 'cdpSCM'
apply plugin: 'maven-publish'

/**
 *  All required repositories path and build dependencies
 */
buildscript {
    apply from: buildscript.sourceFile.parent + "/versions.gradle"

    //resolve dependencies
    repositories {
        maven {
            url repos.mavengoogle
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
        maven { url repos.jcenter }
        maven { url repos.ietrelease }
        maven { url repos.ietsnapshot }
        maven { url repos.plugin
            credentials {
                username = "readonly"
                password = "123qweasdzxc"
            }
        }
    }

    dependencies {
        classpath builddeps.buildinfoextractor
        classpath builddeps.cdpSCM
    }
}

import com.philips.cdp.cdpSCM

def environment = 'production'
if (project.hasProperty('envCode')) {
    environment = envCode
}

project.ext.directdependencies = project.ext.has('useprojectdependencies')

project.ext.objcdp = new cdpSCM()
objcdp.setProjectDir(projectDir)
objcdp.setEnv(platform: 'android', env: environment)


artifactory {
    contextUrl = objcdp.getArtifactoryContextUrl()      //artifactory base url

    publish {
        repository {
            repoKey = objcdp.getArtifactoryPublishRepoKey() //artifactory key to publish to
            username = "320049003"      //publisher user name
            password = "#W3llc0m3"      //publisher password

            defaults {
                publishPom = true
                publishIvy = false
                publishBuildInfo = true
                publishArtifacts = true
                publications('aar')
                properties = ['git.commitid': objcdp.getCommitHashLong()]   //set commit id
            }
        }
    }

    resolve {
        repository {
            repoKey = objcdp.getArtifactoryResolveRepoKey()
        }
    }

    // Artifactory connection timeout (in seconds). The default timeout is 300 seconds.
    clientConfig.timeout = 600
}

/**
 * Resolve dependencies from given url
 */
repositories {
    maven {
        url objcdp.getArtifactoryUrl()
        credentials {
            username = "readonly"
            password = "123qweasdzxc"
        }
    }
}

/**
 * publish aar file to artifactory
 */
publishing {
    publications {
        aar(MavenPublication) {
            groupId group
            version = version
            artifactId archivesBaseName

            artifact("$buildDir/outputs/aar/${archivesBaseName}-release.aar")       //release.aar directory

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.getByName("archives").allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)  //append group id
                        dependencyNode.appendNode('artifactId', it.name)    //append aar name
                        dependencyNode.appendNode('version', it.version)    //append aar version
                    }
                }
            }
        }
    }
}
