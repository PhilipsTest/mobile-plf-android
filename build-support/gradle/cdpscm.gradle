/*
 * Copyright (c) 2015-2018 Koninklijke Philips N.V.
 * All rights reserved.
 */

apply plugin: 'maven-publish'

buildscript {
    apply from: buildscript.sourceFile.parent + "/versions.gradle"

    repositories {
       google()
        jcenter()
        maven {
            url 'https://pkgs.dev.azure.com/PhilipsAgile/_packaging/mobile-plf-android-snapshot-artifacts/maven/v1'
            credentials {
                username "AZURE_ARTIFACTS"
                password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "${azureArtifactsGradleAccessToken}"
            }
        }
    }

    dependencies {
        classpath builddeps.buildinfoextractor
    }
}


publishing {
    publications {
        aar(MavenPublication) {
            groupId "com.philips.platform"
            version = platformversion
            artifactId archivesBaseName

            artifact("$buildDir/outputs/aar/${archivesBaseName}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.getByName("archives").allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
            repositories {
                maven {
                    if(branchName=="develop"){
                        url 'https://pkgs.dev.azure.com/PhilipsAgile/_packaging/mobile-plf-android-snapshot-artifacts/maven/v1'
                        credentials {
                            username "AZURE_ARTIFACTS"
                            password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "${azureArtifactsGradleAccessToken}"
                        }
                    }else if(branchName.startsWith("release/platform")){
                        url 'https://pkgs.dev.azure.com/PhilipsAgile/_packaging/mobile-plf-android-release-artifacts/maven/v1'
                        credentials {
                            username "AZURE_ARTIFACTS"
                            password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "${azureArtifactsGradleAccessToken}"
                        }
                    }

                }
            }
        }
    }
}
